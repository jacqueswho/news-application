plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id("com.google.dagger.hilt.android")
    id("androidx.navigation.safeargs")
    id "org.jlleitschuh.gradle.ktlint" version "11.0.0"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.news.app'

    def localProperties = new Properties()
    localProperties.load(project.rootProject.file("local.properties").newDataInputStream())
    compileSdk rootProject.compiledSdkVersion

    defaultConfig {
        applicationId "com.news.app"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "NEWS_BASE_URL", "\"${localProperties.getProperty("NEWS_BASE_URL","")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "NEWS_API_KEY", "\"" + localProperties['NEWS_API_KEY'] + "\""
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField "String", "NEWS_API_KEY", "\"" + localProperties['NEWS_API_KEY'] + "\""
        }
    }

    ktlint {
        android=true
        debug=true
        ignoreFailures=false
        disabledRules=["no-wildcard-imports","final-newline"]
        reporters {
            reporter "checkstyle"
            reporter "sarif"
            reporter "plain"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    buildFeatures {
        dataBinding true
    }
}

tasks.named("preBuild") { dependsOn("ktlintFormat") }

dependencies {
    implementation 'com.google.android.material:material:1.7.0'

    // Architectural Components
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.core:core-ktx:$ktxCoreVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$loggingInterceptorVersion"

    // Navigation
    implementation("androidx.navigation:navigation-fragment-ktx:$navigationVersion")
    implementation("androidx.navigation:navigation-ui-ktx:$navigationVersion")


    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // Coroutine Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$coroutinesLifeCyleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$coroutinesLifeCyleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensionsVersion"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"

    // Firebase
    implementation "com.google.firebase:firebase-crashlytics-ktx:$firebaseCrashlyticsVersion"
    implementation "com.google.firebase:firebase-analytics-ktx:$firebaseAnaylticsVersion"

    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // Paging
    implementation "androidx.paging:paging-runtime:$pagingVersion"


    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$testExtJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
}

// Allow references to generated code
kapt {
    correctErrorTypes = true
}